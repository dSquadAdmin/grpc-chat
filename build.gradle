buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

plugins {
  id "com.google.protobuf" version "0.8.1"
  id "java"
  id "application"
  id "idea"
}

group 'np.com.keshavbist.chat'
version '1.0.0'

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.3.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'io.grpc:grpc-all:1.4.0'
    compile 'mysql:mysql-connector-java:5.1.6'
    compile 'org.fxmisc.richtext:richtextfx:0.6.10'
    compile 'org.controlsfx:controlsfx:8.40.10'
    compile 'org.slf4j:slf4j-api:1.5.6'
    compile 'org.slf4j:slf4j-simple:1.5.6'
}


sourceSets.main{
    java{
        srcDirs = ["src/generated/main/java", "src/generated/main/grpc", "src/main/java"]
    }
    resources{
        srcDirs "src/resources/"
    }
}

startScripts.enabled = false

task chatServer(type: CreateStartScripts) {
    mainClassName = "np.com.keshavbist.chat.ChatServer"
    applicationName = "chat-Server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}
task chatClient(type: CreateStartScripts) {
    mainClassName = "np.com.keshavbist.chat.ChatClient"
    applicationName = "chat-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(chatServer)
    from(chatClient)
    fileMode = 0755
}
